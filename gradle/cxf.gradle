configurations { cxf }

dependencies {
	def cxfGroup = 'org.apache.cxf'
    def cxfVersion = '3.0.1'
    cxf cxfGroup + ':cxf-tools-wsdlto-core:' + cxfVersion
    cxf cxfGroup + ':cxf-tools-wsdlto-frontend-jaxws:' + cxfVersion
    cxf cxfGroup + ':cxf-tools-wsdlto-databinding-jaxb:' + cxfVersion
}

task wsdl2java(type: JavaExec) {
	ext {
        wsdlFile = ''
		outputDir = file("$projectDir/generated-src")
	}

	// Main Java class to invoke.
	main = 'org.apache.cxf.tools.wsdlto.WSDLToJava'

	// Set classpath to dependencies assigned
	// to the cxf configuration.
	classpath = configurations.cxf

	// Arguments to be passed to WSDLToJava.
	args '-d', outputDir
	args '-client'
	args '-verbose'
	args '-validate'

    project.gradle.projectsEvaluated {
        inputs.property('wsdlFile', wsdlFile)
        outputs.dir outputDir
        args wsdlFile
	}

    doFirst {
        delete outputDir
    }
}

clean.delete wsdl2java.outputDir
